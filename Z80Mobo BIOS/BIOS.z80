;  Origionally from some old OS design I made.
; Then became MinOS
; Kernel reused in NTIOS 0.1
; Kernel heavily modified NTIOS 0.2
; @1903191502 Rewritten to be a BIOS instead
; Copyright Dylan Brophy 2017
;
org $0000
; CONFIGURATION

reboot: equ bios_start

stack_top:     equ $FFFF
text_rows:		equ 20
free_text_rows:	equ text_rows-3

vectors:
	jp bios_start ; 0000 (reboot)
	jp print    ; 0003
	jp println  ; 0006
	jp alloc    ; 0009
	jp 0;delete   000C
	jp strcomp  ; 000F
	jp 0;input    ; 0012
	jp 0000     ; 0015
	jp 0000;endpgm   ; 0018
	
	jp 0000     ; 001B
	jp 0000     ; 001E
	jp 0000     ; 0021
	jp 0000     ; 0024
	nop
	jp 0000     ; 0028
	jp 0000     ; 002B
	jp 0000     ; 002E
	jp 0000     ; 0031
	jp 0000     ; 0034
nop             ; 0037
int38:          ; 0038, Z80 interrupt mode 1 vector
	push hl
	push af
	push de
	push bc
	push ix
	push iy
	;jp 55 ; lol
	
	pop iy
	pop ix
	pop bc
	pop de
	pop af
	pop hl
	reti
bios_start:
	di
	ld sp, stack_top
	ld hl, 0
	
	call memory_init
	
	call _uart_setup
	ld (term_inpipe), hl
	ld (term_outpipe), hl
	
	ld hl, .loadmsg
	call println
	
	call os_setup_tracer
	call init_pipe_list
	call init_bd_list
	call pipeserver_setup
	
	; system should be completely set up at this point.
	

	; allow user to select a terminal by iterating through pipes and
	; sending a login to each, then see which responds first.
	ld de, .rqaccessmsg
	ld hl, pipe_list_cnt
	ld c, (hl)
	ld hl, pipe_list
.rq_access_loop:
	push hl
	ld a, (hl)
	inc hl
	ld h, (hl)
	ld l, a
	call pipe.println
	call pipe.clean
	pop hl
	dec c
	dec c
	jp z, .did_rq
	inc hl
	inc hl
	jp .rq_access_loop
.did_rq:
	; sent message, now just iterate through looking for a response.
	ld hl, pipe_list_cnt
	ld c, (hl)
	ld hl, pipe_list
.check_avail_loop:
	push hl
	ld a, (hl)
	inc hl
	ld h, (hl)
	ld l, a
	call pipe.available
	pop hl
	or a
	jp nz, .found_terminal
	dec c
	dec c
	jp z, .did_rq ; if we looped through all and none are available, try again.  Forever.  Because one day SOMEONE WILL PRESS DAT KEY!!!
	inc hl
	inc hl
	jp .check_avail_loop
.found_terminal:
	ld a, (hl)
	inc hl
	ld h, (hl)
	ld l, a
	ld (term_inpipe), hl
	ld (term_outpipe), hl
	ld a, (pipe_list_cnt)
	sub c
	rra
	and $1F
	call byte_to_hex
	ld d, h
	ld e, l
	ld hl, pipe_list_cnt
	ld c, (hl)
	ld hl, pipe_list
.term_selected_loop:
	push hl
	ld a, (hl)
	inc hl
	ld h, (hl)
	ld l, a
	push de
	ld de, .termselmsg
	call pipe.println
	pop de
	call pipe.println
	pop hl
	dec c
	dec c
	jp z, .term_selected
	inc hl
	inc hl
	jp .term_selected_loop
.term_selected:
	ld hl, .termactmsg
	call println
	ld hl, .lookingfordrivesmsg
	call println
	
	call upd7220_setup
	
	ld hl, 128
	call alloc ; allocate 128 bytes on the heap for keyboard input
	push hl
	
.ioloop:
	ld hl, .adrmsg
	call print
	
	pop hl
	call input
	call hex_to_byte
	ld b, 0
	ld c, a
	
	push hl
	ld hl, .datmsg
	call print
	
	pop hl
	call input
	push hl
	xor a
	cp (hl)
	jp z, .io_read
	call hex_to_byte
	
	out (c), a
	jp .ioloop

.io_read:
	in a, (c)
	pop hl
	call byte_to_hex
	push hl
	call println
	jp .ioloop
	
	halt



.testmsg: db "[Test Message]", 0
.loadmsg: db "Loading BIOS...",0
.donemsg: db "Done!", 0

.adrmsg: db "adr> ", 0
.datmsg: db "dat> ", 0
	
.rqaccessmsg: db "Press any key to activate this terminal.",0
.termactmsg: db "Terminal Activated.", 0
.termselmsg: db "Terminal was activated: ", 0

.lookingfordrivesmsg: db "Checking disks...",0
	
system_error:
	ld hl, bios_broken
	call try
	
	ld hl, $8000
	call print_memory
	ld hl, $8100
	call print_memory
	ld hl, $8200
	call print_memory
	call traceback
	ld a, 13
	call chrout
	ld hl, .msg
	call println
	call input
	jp 0
.msg: db "Computer will not recover, press enter to reboot.", 0
bios_broken:
	ld hl, .emergency
	call try
	
	ld hl, uart_pipe_obj ; switch back to UART pipe, in case it fixes the terminal (if that's even what's wrong...)
	ld (term_outpipe), hl
	ld (term_inpipe), hl
	
	ld hl, $8000
	call print_memory
	ld hl, $8100
	call print_memory
	ld hl, $a300
	call print_memory
	
	call traceback
.emergency:
	ld hl, .msg
	call _uart_println ; in case terminal IO is doomed
	halt
.msg: db "Serious BIOS Error.  Please reflash the bios and force reboot of machine.", 0
INCLUDE "string.z80"
INCLUDE "objutil.z80"
INCLUDE "uart.z80"
INCLUDE "memory.z80"
INCLUDE "ui.z80"
INCLUDE "traceback.z80"
INCLUDE "pipeserver.z80"
INCLUDE "upd7220.z80"
empty_str: db 0

heap_top:		equ $8002
tmp:			equ $8004
term_inpipe:	equ $8006
term_outpipe:	equ $8008
current_pipe:	equ $800A
catch_stack:	equ $8105
tmp_sp:			equ $8106
tmp_sp2:		equ $8108
catch_sp:		equ $8108
pipe_list_cnt:	equ $810A
pipe_list:		equ $810C
current_bd:		equ $8200 ; bd stands for Block Device
bd_list_cnt:	equ $8202
bd_list:		equ $8204
main_disk:		equ $8240


BIOS_END: